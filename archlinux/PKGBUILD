# Maintainer: Danil Osherov <shindo@yandex-team.ru>
pkgname=handystats
pkgver=1.11.3
pkgrel=1
pkgdesc="C++ library for collecting user-defined in-process runtime statistics with low overhead"
arch=('any')
url=('https://github.com/shindo/handystats')
license=('LGPL3')
depends=('boost>=1.46' 'rapidjson>=1.0.0')
checkdepends=('gtest')
makedepends=('git' 'cmake')
source=("$pkgname"::"git://github.com/shindo/handystats.git#tag=$pkgver")
md5sums=('SKIP')

build() {
	cd "$srcdir/$pkgname"
	cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/usr
	make
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname"
	make install
}
